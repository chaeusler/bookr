subprojects {
    apply plugin: 'java'
    sourceCompatibility = 1.8

    repositories {
        mavenCentral()
    }

    configurations {
        apiOutput
        testOutput.extendsFrom testCompile
    }

    task apiJar(type: Jar, dependsOn: classes) {
        from sourceSets.main.output
        classifier = 'api'
        include "${project.name}/api/**"
    }

    task testJar(type: Jar, dependsOn: testClasses) {
        from sourceSets.test.output
        classifier = 'test'
    }

    jar {
        exclude "${project.name}/api/**"
    }

    artifacts {
        apiOutput apiJar
        testOutput testJar
    }

    dependencies {
        //testCompile 'junit:junit:4.11'
    }
}


apiDependency 'booking', 'person'
apiDependency 'booking', 'project'
apiDependency 'project', 'person'
apiDependency 'autorization', 'person'
//testDependency 'module2', 'module1'

def apiDependency(String module, String dependsOnModule){
    project(":modules:${module}") {
        dependencies {
            compile project(path: ":modules:${dependsOnModule}", configuration: 'apiOutput')
        }
    }
}

def testDependency(String module, String dependsOnModule){
    project(":modules:${module}") {
        dependencies {
            testCompile project(path: ":modules:${dependsOnModule}", configuration: 'testOutput')
        }
    }
}

